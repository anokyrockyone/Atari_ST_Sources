1     ' *****************************
2     ' *                           *
3     ' *          1_print          *
4     ' *                           *
5     ' *   Kassettenhuellendruck   *
6     ' *     + PUBLIC DOMAIN +     *
7     ' *            by             *
8     ' *                           *
9     ' *       Stephan Meyer       *
10    '*                           *
11    '*****************************
15    DIM A$(50,2),E$(50)
20    FULLW 2:GOSUB FULLWIND
30    GOSUB CLEARDESK
40    GOSUB CLE
50    FOR X=1 TO 46
60    E$(X)=""
70    NEXT
120   GOTOXY 0,4
130   PRINT "Rauschunterdrueckungssystem :"
140   ?:?:?"1) DOLBY C"
150   ?:?"2) DOLBY B
160   ?:?"3) DOLBY HX"
170   ?:?"4) dbx"
180   ?:?"5) HIGH-COM"
185   ?:?"6) Kein Rauschunterdrueckungssystem"
190   RL:?:?:INPUT"Ihre Wahl ";RR
200   ON RR GOTO R1,R2,R3,R4,R5,R6
210   GOTO RL
220   R1:R$=" DOLBY C":GOTO EN
230   R2:R$=" DOLBY B":GOTO EN
240   R3:R$="DOLBY HX":GOTO EN
250   R4:R$="     dbx":GOTO EN
260   R5:R$="HIGH-COM":GOTO EN
270   R6:R$="        "
280   EN:E$(1)=STRING$(60," ")+R$
300   FOR Y=1 TO 2
310   GOSUB CLE
320   GOTOXY 0,4
325   ?"                                 seite ";y
327   FOR X=1 TO 28 STEP 2
330   XX=INT(X/2)+1
340   GOTOXY 0,8:?"    Titel Nr.";XX;" ";:INPUT A$(X,Y)
345   IF LEN(A$(X,Y))>30 GOTO 340
350   GOTOXY 0,12:?"     Info Nr.";XX;" ";:INPUT A$(X+1,Y)
355   IF LEN(A$(X+1,Y))>30 GOTO 350
360   NEXT X,Y
362   GOSUB CLE
366   GOTOXY 1,4:?"Haupttitel :"
368   GOTOXY 1,8:INPUT "ZEILE 1 ";Z$(1)
369   REM IF LEN(Z$(1))>39 GOTO 368
370   GOTOXY 1,10:INPUT"ZEILE 2 ";Z$(2)
371   REM IF LEN(Z$(2))>39 GOTO 370
372   IF Z$(2)="" THEN ZZ=1 ELSE ZZ=0
374   GOTOXY 1,14:INPUT"Titel zentriert (j/n) ";Q$
376   IF Q$="J" OR Q$="j" THEN CC=1 ELSE CC=0
380   GOSUB CLE
382   GOTOXY 1,4:?"Message :"
384   FOR T=1 TO 2
386   GOTOXY 1,4+T*2:?"ZEILE ";T;" ";:INPUT Z$(T+2)
387   IF LEN(Z$(T+2))>39 GOTO 386
388   NEXT
400   REM KOORDINATION TITEL
405   H$="    ":U=0
410   FOR X=1 TO 28 STEP 2
415   U=U+1
420   FOR Y=1 TO 2
425   IF A$(X,Y)="" THEN F$(Y)="    " ELSE F$(Y)=STR$(U)+" "
427   IF LEN(F$(Y))=3 THEN F$(Y)=" "+F$(Y)
429   FOR W=0 TO 1
430   L=30-LEN(A$(X+W,Y))
440   A$(X+W,Y)=A$(X+W,Y)+STRING$(L," ")
450   NEXT W,Y
460   E$(X+2)=F$(1)+A$(X,1)+F$(2)+A$(X,2)
465   E$(X+3)=H$+A$(X+1,1)+H$+A$(X+1,2)
470   NEXT
500   REM KOORDINATION ZEILE
502   FOR T=1 TO 4
503   IF CC=0 AND T<3 GOTO SPRUNG
504   L=(39-LEN(Z$(T)))/2
506   Z$(T)=STRING$(L," ")+Z$(T)+STRING$(L," ")
508   SPRUNG: IF LEN(Z$(T))<39 THEN LET Z$(T)=Z$(T)+" ":GOTO SPRUNG
510   NEXT
520   IF ZZ=1 THEN E$(35)=Z$(1) ELSE E$(34)=Z$(1):E$(36)=Z$(2)
522   E$(41)=Z$(3)
524   E$(44)=Z$(4)
530   IF ZZ=1 THEN E$(35)=Z$(1) ELSE E$(34)=Z$(1):E$(36)=Z$(2)
600   REM SPEZIELLE ZEILEN
610   L$=STRING$(70,"_")
620   E$(2)=" A:"+STRING$(32," ")+"B:"+STRING$(31," ")
630   E$(38)=STRING$(68,"_")
640   E$(32)=E$(38)
650   E$(46)=E$(38)
700   REM AUFFUELLEN DER LEERZEILEN
710   C$=STRING$(68," ")
720   FOR X=1 TO 46
730   IF LEN(E$(X))=0 THEN E$(X)=C$
740   NEXT
800   GOSUB CLE
810   GOTOXY 14,10:?"==  DRUCKEN  =="
1000  REM DRUCKEN
1005  II=1
1010  LPRINT CHR$(27)+CHR$(15);:' KOMPRIMIERT
1020  LPRINT CHR$(27)+"S"+CHR$(0);:' SUPERSCRIPT
1030  LPRINT CHR$(27)+"3"+CHR$(17);:' LINE SPACING
1040  FOR X=1 TO 4:LPRINT:NEXT
1100  LPRINT L$
1110  FOR X=1 TO 46
1120  LPRINT CHR$(&H04);
1140  rem IF X=32 THEN GOSUB UNDER
1150  IF X=34 OR  X=36 THEN GOSUB HEAD
1155  IF X=35 THEN GOSUB MHEAD
1160  IF X=41 OR X=44 THEN GOSUB MESS
1200  IF II=1 THEN LPRINT E$(X); ELSE II=1
1210  LPRINT CHR$(&H04)
1300  NEXT
1999  END
2000  '___________________________
2010  '
2020  UNDER:
2030  LPRINT E$(X);
2040  FOR S=1 TO 68
2050  LPRINT CHR$(8);
2060  NEXT
2070  FOR S=1 TO 68
2080  LPRINT "_";
2090  NEXT
2095  II=0
2100  RETURN
2110  HEAD:
2115  IF ZZ=1 THEN RETURN
2120  LPRINT CHR$(27)+"T";
2130  LPRINT CHR$(18);
2140  LPRINT CHR$(27)+"4";
2141  LPRINT CHR$(27)+"E";
2145  LPRINT CHR$(27)+"n";
2150  LPRINT E$(X);
2155  LPRINT CHR$(27)+"P";
2159  LPRINT CHR$(27)+"F";
2160  LPRINT CHR$(27)+"5";
2170  LPRINT CHR$(27)+"S"+CHR$(0);
2180  LPRINT CHR$(15);
2181  LPRINT CHR$(32);
2185  II=0
2190  RETURN
2200  MESS:
2210  LPRINT CHR$(27)+"T";
2220  LPRINT CHR$(18);
2230  LPRINT CHR$(27)+"4";
2235  LPRINT CHR$(27)+"n";
2240  LPRINT E$(X);
2245  LPRINT CHR$(27)+"P";
2250  LPRINT CHR$(27)+"5";
2260  LPRINT CHR$(27)+"S"+CHR$(0);
2270  LPRINT CHR$(15);
2271  LPRINT CHR$(32);
2280  II=0
2290  RETURN
2310  MHEAD:
2315  IF ZZ=0 THEN RETURN
2320  LPRINT CHR$(27)+"T";
2330  LPRINT CHR$(18);
2340  LPRINT CHR$(27)+"4";
2341  LPRINT CHR$(27)+"E";
2345  LPRINT CHR$(27)+"n";
2350  LPRINT E$(X);
2355  LPRINT CHR$(27)+"P";
2359  LPRINT CHR$(27)+"F";
2360  LPRINT CHR$(27)+"5";
2370  LPRINT CHR$(27)+"S"+CHR$(0);
2380  LPRINT CHR$(15);
2381  LPRINT CHR$(32);
2385  II=0
2390  RETURN
10000 CLE:CLEARW 2
10010 SCHRIFTTYP=16:GOSUB SCHRIFTART
10020 GROESSE=20:GOSUB SCHRIFTGROESSE
10030 XPOS=200:YPOS=20:TEXT$="Print-o-kett"
10040 GOSUB TX
10050 SCHRIFTTYP=4:GOSUB SCHRIFTART
10060 GROESSE=10:GOSUB SCHRIFTGROESSE
10070 RETURN
62022 '--------------------------
62050 FULLWIND:
62052 ' 
62053 GOSUB INIT
62054 POKE GINTIN,3
62056 POKE GINTIN+2,5
62058 POKE GINTIN+4,0
62060 POKE GINTIN+6,1
62062 POKE GINTIN+8,658
62064 POKE GINTIN+10,417
62066 GEMSYS 105
62068 RETURN
62070 '--------------------------
62080 CLEARDESK:
62082 ' uses RECHTECK.BAS
62084 COLOR 1,0,1:
62086 XPOS1=-1:XPOS2=638:YPOS1=-40:YPOS2=-20
62088 GOSUB RECHTECK:COLOR 1,1,1
62090 RETURN
62092 '--------------------------
62900 INIT:
62902 AES#=GB
62904 CONTROL =PEEK(AES#)
62906 GLOBAL  =PEEK(AES#+4)
62908 GINTIN  =PEEK(AES#+8)
62910 GINTOUT =PEEK(AES#+12)
62912 ADDRIN  =PEEK(AES#+16)
62914 ADDROUT =PEEK(AES#+20)
62916 RETURN
62918 '--------------------------
64000 RECHTECK:
64002 ' -->XPOS1; YPOS1; XPOS2; YPOS2
64004 POKE CONTRL,11
64006 POKE CONTRL+2,2
64008 POKE CONTRL+6,0
64010 POKE CONTRL+10,1
64012 POKE PTSIN,XPOS1+1
64014 POKE PTSIN+2,YPOS1+38
64016 POKE PTSIN+4,XPOS2+1
64018 POKE PTSIN+6,YPOS2+38
64020 VDISYS
64022 RETURN
64024 '--------------------------
65000 SCHRIFTART:
65002 ' --> SCHRIFTTYP
65004 '     0=NORMAL      1=FETT      2=HELL
65006 '     4=KURSIV      8=UNTERSTR.16=HOHL
65008 '     oder Kombinationen
65010 '
65012 POKE CONTRL,106
65014 POKE CONTRL+2,0
65016 POKE CONTRL+6,1
65018 POKE INTIN,SCHRIFTTYP
65020 VDISYS
65022 RETURN
65030 SCHRIFTGROESSE:
65032 ' --> GROESSE
65034 '     <9   SEHR KLEIN              9 KLEIN
65036 '  10-15   NORMAL              16-18 GROSS            
65038 '  18-19   SEHR GROSS
65040 '
65042 POKE CONTRL,107
65044 POKE CONTRL+2,0
65046 POKE CONTRL+6,1
65048 POKE INTIN,GROESSE
65050 VDISYS
65052 RETURN
65060 TX:
65062 ' --> TEXT $ ,  XPOS ,  YPOS
65064 ' massstabsgerechte Textausgabe
65066 FOR I=0 TO LEN(TEXT$)-1
65068 POKE INTIN+I*2,ASC(MID$(TEXT$,I+1,1))
65070 NEXT
65072 POKE INTIN+I*2,0
65074 POKE CONTRL,8
65076 POKE CONTRL+2,1
65078 POKE CONTRL+6,LEN(TEXT$)+1
65080 POKE PTSIN,XPOS+1
65082 POKE PTSIN+2,YPOS+38
65084 VDISYS
65086 RETURN
