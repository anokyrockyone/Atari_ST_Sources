1     ' *****************************
2     ' *                           *
3     ' *            pi             *
4     ' *                           *
5     ' *      3D - Graphiken       *
6     ' *     + PUBLIC DOMAIN +     *
7     ' *            by             *
8     ' *                           *
9     ' *       Stephan Meyer       *
10    '*                           *
11    '*****************************
15    PI=3.141592654
20    FULLW 2:GOSUB INIT
30    GOSUB FULLWIND:GOSUB CLEARDESK:CLEARW 2
40    GOTOXY 3,3:?"MENUE :"
50    GOTOXY 3,5:?"1) HUT"
60    GOTOXY 3,6:?"2) ERDBEBEN"
70    GOTOXY 3,7:?"3) TIEFSCLAG"
80    GOTOXY 3,8:?"4) LOCH"
90    GOTOXY 3,9:?"5) KRATER"
100   GOTOXY 3,10:?"6) RADNABE"
110   GOTOXY 3,11:?"7) QUIT"
120   GOTOXY 3,13:INPUT"IHRE WAHL  ";AA
130   CLEARW 2:ON AA GOTO 150,160,170,180,190,200,140
140   END
150   XR=1.5*PI:YR=1:GOTO 210
160   XR=2.5*PI:YR=5:GOTO 210
170   XR=2.085*PI:YR=.7*PI:GOTO 210
180   XR=1.5*PI:YR=-2:GOTO 210
190   XR=1.5*PI:YR=-3:GOTO 210
200   XR=3.5*PI:YR=5:GOTO 210
210   P=512:Q=450
220   XP=460
230   YP=179:ZP=205
240   XF=XR/XP:YF=YP/YR:ZF=XR/ZP
250   FOR ZI=-Q TO Q-1 STEP 6.4
260   IF ZI<-ZP OR ZI>ZP THEN 340
270   ZT=ZI*XP/ZP:ZZ=ZI
280   XL=INT(.5+SQR(XP*XP-ZT*ZT))
290   FOR XI=-XL TO XL STEP 6.4
300   XT=SQR(XI*XI+ZT*ZT)*XF:XX=XI
310   YY=(SIN(XT)+.4*SIN(3*XT))*YF
320   GOSUB 360
330   NEXT XI
340   NEXT ZI
350   A=INP(2):GOTO 20
360   X1=(XX+ZZ+P+450)*2
370   Y1=(1000-(YY-ZZ+Q)-100)*2
380   LINEF X1/5.12,Y1/4.4,X1/5.12,Y1/4.4
390   IF Y1=0 THEN 440
400   COLOR 1,1,0
410   LINEF X1/5.12,Y1/4.4+1,X1/5.12,400
420   LINEF X1/5.12+1,Y1/4.4+1,X1/5.12+1,400
430   COLOR 1,1,1
440   RETURN
450   SETWIND:
460   ' --> XPOS; YPOS; BREIT
470   ' --> HOCH; FENSTERNR
480   POKE GINTIN,FENSTERNR
490   POKE GINTIN+2,5
500   POKE GINTIN+4,XPOS
510   POKE GINTIN+6,YPOS
520   POKE GINTIN+8,BREIT
530   POKE GINTIN+10,HOCH
540   GEMSYS 105
550   RETURN
560   '--------------------------
570   FULLWIND:
580   ' 
590   POKE GINTIN,3
600   POKE GINTIN+2,5
610   POKE GINTIN+4,0
620   POKE GINTIN+6,1
630   POKE GINTIN+8,658
640   POKE GINTIN+10,417
650   GEMSYS 105
660   RETURN
670   '--------------------------
680   CLEARDESK:
690   ' uses RECHTECK.BAS
700   COLOR 1,0,1:
710   XPOS1=-1:XPOS2=638:YPOS1=-40:YPOS2=-20
720   GOSUB RECHTECK:COLOR 1,1,1
730   RETURN
740   '--------------------------
750   INIT:
760   AES#=GB
770   CONTROL =PEEK(AES#)
780   GLOBAL  =PEEK(AES#+4)
790   GINTIN  =PEEK(AES#+8)
800   GINTOUT =PEEK(AES#+12)
810   ADDRIN  =PEEK(AES#+16)
820   ADDROUT =PEEK(AES#+20)
830   RETURN
840   '--------------------------
850   RECHTECK:
860   ' -->XPOS1; YPOS1; XPOS2; YPOS2
870   POKE CONTRL,11
880   POKE CONTRL+2,2
890   POKE CONTRL+6,0
900   POKE CONTRL+10,1
910   POKE PTSIN,XPOS1+1
920   POKE PTSIN+2,YPOS1+38
930   POKE PTSIN+4,XPOS2+1
940   POKE PTSIN+6,YPOS2+38
950   VDISYS
960   RETURN
970   '--------------------------
