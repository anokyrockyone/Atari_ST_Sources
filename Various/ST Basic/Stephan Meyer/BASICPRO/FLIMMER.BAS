1     ' *****************************
2     ' *                           *
3     ' *          flimmer          *
4     ' *                           *
5     ' * Demo der STERN-Subroutine *
6     ' *     + PUBLIC DOMAIN +     *
7     ' *            by             *
8     ' *                           *
9     ' *       Stephan Meyer       *
10    '*                           *
11    '*****************************
15    CLEARW 2:FULLW 2
20    COLOR 1,1,1,8,2
30    YPOS=200:XPOS=300
40    RADIUS=100
50    F=0
60    FOR T=10TO 15
70    ECKEN=T
80    WHILE NOT F>(T/2)
90    F=F+0.05
110   GOTOXY 0,2:?T;F
120   PHI=3.141593/2/ECKEN*F
130   GOSUB STERN
132   COLOR 0,0,0,0,0
134   GOSUB STERN
136   COLOR 1,1,1,8,2
140   WEND
150   F=0
160   NEXT T
170   END
63400 POLYMARKER:
63402 '--> XPOS, YPOS
63404 '--> MARKERTYP, MARKERGROESSE
63406 '
63408 POKE CONTRL,18
63410 POKE CONTRL+2,0
63412 POKE CONTRL+6,1
63414 POKE INTIN,MARKERTYP
63416 VDISYS
63418 POKE CONTRL,19
63420 POKE CONTRL+2,1
63422 POKE CONTRL+6,0
63424 POKE PTSIN,0
63426 POKE PTSIN+2,MARKERGROESSE
63428 VDISYS
63430 '
63432 MARKERSETZEN:
63434 ' ( --> XPOS, YPOS )
63436 POKE CONTRL,7
63438 POKE CONTRL+2,1:' ODER MEHR
63440 POKE PTSIN,XPOS+1
63442 POKE PTSIN+2,YPOS+38
63444 VDISYS
63446 RETURN
63466 '--------------------------
64100 VIELECK:
64102 ' --> XPOS, YPOS
64104 ' --> RADIUS, ECKEN
64106 PHI=3.141593/ECKEN/2
64108 STERN:
64110 ' --> PHI
64112 POKE CONTRL,9
64114 POKE CONTRL+6,0
64116 POKE CONTRL+2,ECKEN
64118 FOR ECK=0 TO ECKEN*4 STEP 4
64120 POKE PTSIN+ECK   , 1+XPOS+COS(PHI*ECK)*RADIUS
64122 POKE PTSIN+(ECK+2),38+YPOS+SIN(PHI*ECK)*RADIUS
64124 NEXT
64126 VDISYS
64128 RETURN
64130 '--------------------------
64140 POLYGON:' ** fuellflaeche **
64142 ' --> xkoord(n),ykoord(n),ecken[~n.MAX]
64144 POKE CONTRL,9
64146 POKE CONTRL+6,0
64148 POKE CONTRL+2,ECKEN
64150 FOR V=1 TO ECKEN
64152 POKE PTSIN+(V-1)*4,XKOORD(V)+1
64154 POKE PTSIN+2+(V-1)*4,YKOORD(V)+38
64156 NEXT
64158 VDISYS
64160 RETURN
64162 '--------------------------
64300 POLYLINE:
64301 ' --> xkoord(n), ykoord(n)
64302 POKE CONTRL,6
64304 POKE CONTRL+6,0
64306 POKE CONTRL+2,ANZAHL
64308 FOR I=0 TO ANZAHL
64310 POKE PTSIN+I*4,XKOORD(I)+1
64312 POKE PTSIN+2+I*4,YKOORD(I)+38
64314 NEXT
64316 VDISYS
64318 RETURN
64320 '--------------------------
65130 GRAFIKMODUS:
65132 ' -->MODUS
65134 '    1=UEBERSCHREIBEN         2=MISCHEN
65136 '    3=XOR- VERKNUEPFEN       4=REVERS,MISCHEN
65138 POKE CONTRL,32
65140 POKE CONTRL+2,0
65142 POKE CONTR+6,1
65144 POKE INTIN,MODUS
65146 VDISYS
65148 RETURN
65150 '--------------------------
