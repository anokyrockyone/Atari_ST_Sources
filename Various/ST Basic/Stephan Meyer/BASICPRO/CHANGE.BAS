1     ' *****************************
2     ' *                           *
3     ' *          change!          *
4     ' *                           *
5     ' *    Zeichnungs-Umwandler   *
6     ' *     + PUBLIC DOMAIN +     *
7     ' *            by             *
8     ' *                           *
9     ' *       Stephan Meyer       *
10    '*                           *
11    '*****************************
13    GOSUB INIT
14    FULLW 2:CLEARW 2:GOSUB FULLWIND
15    COLOR 1,1,1
20    L%=50
30    WI%=640
40    HI%=400
50    CX%=WI%/2
60    CY%=HI%/2
70    MX%=WI%/100
80    MY%=HI%/100
90    DIM X%(L%,2),Y%(L%,2)
100   CLEARW 2:GOSUB MAUSFORM
110   GOSUB 760
120   GOTOXY 3,3:?"MENUE :"
130   GOTOXY 3,5:?"1) ZEICHNEN"
140   GOTOXY 3,6:?"2) BETRACHTEN"
145   GOTOXY 3,7:?"3) ENDE"
150   D%=VAL(CHR$(INP(2)))
155   CLEARW 2
157   IF D%=3 THEN END
160   X%=10
170   Y%=CY%
180   LINEF X%,Y%,X%,Y%
190   IF D%=1 THEN GOSUB 220
200   IF D%=2 THEN GOSUB 480
210   GOTO 120
220   FOR J%=1 TO 2
230   I%=1:V%=0:W%=0
250   GOSUB 690
260   GOSUB MAUS
310   LINIENTYP=C3(J%):GOSUB LINIENMUSTER
380   LINEF X%,Y%,X%+1,Y%:X2%=X%:Y2%=Y%
390   IF KEY<>0 THEN GOSUB 640
410   GOSUB 730
420   COLOR 1,1,0:LINEF X%,Y%,X%+1,Y%:COLOR 1,1,1
430   V%=0
440   W%=0
450   IF I%<L%+1 GOTO 260
460   NEXT
465   CLEARW 2
470   RETURN
480   GOTOXY 3,10:INPUT"ANZAHL DER STUFEN ";F%:F%=F%-1
485   CLEARW 2
490   FOR Z%=0 TO F%
500   GOTOXY 0,0:?"FIGUR NR."Z%+1
510   X%=(X%(1,1)*(F%-Z%)+X%(1,2)*Z%)/F%
520   Y%=(Y%(1,1)*(F%-Z%)+Y%(1,2)*Z%)/F%
530   LINEF X%,Y%,X%,Y%:X1%=X%:Y1%=Y%
540   FOR I%=1 TO L%
550   X%=(X%(I%,1)*(F%-Z%)+X%(I%,2)*Z%)/F%
560   Y%=(Y%(I%,1)*(F%-Z%)+Y%(I%,2)*Z%)/F%
570   LINIENTYP=C1:GOSUB LINIENMUSTER
580   IF Z%<1 OR Z%>F%-1 THEN LINIENTYP=C2:GOSUB LINIENMUSTER
590   LINEF X1%,Y1%,X%,Y%
595   X1%=X%:Y1%=Y%
600   NEXT 
610   GOSUB 710
620   NEXT
625   A%=INP(2):CLEARW 2
630   RETURN
640   X%(I%,J%)=X%
650   IF I%=1 THEN LINEF X%,Y%+1,X%+1,Y%+1
660   Y%(I%,J%)=Y%
670   IF I%>1 THEN LINEF X2%,Y2%,X%(I%-1,J%),Y%(I%-1,J%)
680   I%=I%+1
690   GOTOXY 0,1:?"FIGUR "J%": FREIE PUNKTE "L%+1-I%
700   RETURN
710   FOR T%=1 TO 2000
720   NEXT
730   FOR T%=1 TO 100
740   NEXT
750   RETURN
760   C1=4
770   C2=1
780   C3(1)=5
790   C3(2)=2
800   RETURN
62050 FULLWIND:
62052 ' 
62054 POKE GINTIN,3
62056 POKE GINTIN+2,5
62058 POKE GINTIN+4,0
62060 POKE GINTIN+6,1
62062 POKE GINTIN+8,658
62064 POKE GINTIN+10,417
62066 GEMSYS 105
62068 RETURN
62070 '--------------------------
62900 INIT:
62902 AES#=GB
62904 CONTROL =PEEK(AES#)
62906 GLOBAL  =PEEK(AES#+4)
62908 GINTIN  =PEEK(AES#+8)
62910 GINTOUT =PEEK(AES#+12)
62912 ADDRIN  =PEEK(AES#+16)
62914 ADDROUT =PEEK(AES#+20)
62916 RETURN
62918 '--------------------------
63000 MAUS:
63004 POKE CONTRL,124
63006 VDISYS
63008 X%=PEEK(PTSOUT)+1
63010 Y%=PEEK(PTSOUT+2)-19
63012 KEY=PEEK(INTOUT)
63014 RETURN
63280 MAUSFORM:
63288 ADD#=GB
63290 GINTIN=PEEK(ADD#+8)
63292 ADDRIN=PEEK(ADD#+16)
63294 POKE GINTIN,5
63296 POKE ADDRIN,0
63298 GEMSYS(78)
63300 RETURN
64000 RECHTECK:
64002 ' -->XPOS1; YPOS1; XPOS2; YPOS2
64004 POKE CONTRL,11
64006 POKE CONTRL+2,2
64008 POKE CONTRL+6,0
64010 POKE CONTRL+10,1
64012 POKE PTSIN,XPOS1+1
64014 POKE PTSIN+2,YPOS1+38
64016 POKE PTSIN+4,XPOS2+1
64018 POKE PTSIN+6,YPOS2+38
64020 VDISYS
64022 RETURN
64024 '--------------------------
64240 LINIENMUSTER:
64242 ' --> LINIENTYP
64244 ' 1-7
64246 POKE CONTRL,15
64248 POKE CONTRL+2,0
64250 POKE CONTRL+6,1
64252 POKE INTIN,LINIENTYP
64254 VDISYS
64256 RETURN
